---- MODULE ShareChain ----
EXTENDS Naturals, Sequences, TLC, FiniteSets, Integers, SequencesExt

CONSTANTS 
    Processes,                  \* Set of processes that generate shares
    MaxShares,                  \* Maximum number of shares per process
    Work,                       \* Work/difficulty parameter for shares
    MaxUnclesDepth,             \* Maximum depth for uncles included in a share
    MaxBitcoinHeight            \* Current Bitcoin network height for validating received shares

VARIABLES
    shares,                     \* Set of all shares across all processes
    parent,                     \* Parent references for each share
    uncles,                     \* Uncle references for each share
    confirmed_work,             \* Total work accumulated up to each confirmed height on each process
    candidate_work,             \* Total work accumulated up to each candidate height, if any
    expected_height,            \* expected height of each share in the chain at each process. Shares may or may not candidate or confirm to this height
    candidates,                 \* candidates of each share in the chain at each process
    confirmed,                  \* confirmed of each share in the chain at each process
    candidate_uncles,           \* candidate uncles height of each share in the chain at each process
    confirmed_uncles,           \* confirmed uncles height of each share in the chain at each process
    received_shares,            \* Queue of shares received from other processes, but not validated yet
    validation_status,          \* Validation status of received shares
    share_queue,                \* Queue of shares that have been generated but not yet sent to peers,
    bitcoin_height,             \* Bitcoin network height associated with each share
    spend_dependencies          \* Dependency Share -> Share. The key share is spending the value share.
vars == <<shares, share_queue, uncles, parent, confirmed_work, bitcoin_height, confirmed, candidates, received_shares, validation_status, spend_dependencies, candidate_work, confirmed_uncles, candidate_uncles, expected_height>>

(****************************************************************************)
(* Genesis share definition                                                 *)
(****************************************************************************)

Genesis == [process |-> CHOOSE p \in Processes: TRUE, work |-> 1, bitcoin_height |-> 0]

\* Use NoHeight to indicate share not found in height sequences, aligns with sequence indexed from 1
NoHeight == 0

TotalShares == MaxShares * Cardinality(Processes)

\* Genesis height is 1, aligns with sequence indexed from 1
Height == (1..TotalShares)

(****************************************************************************)
(* A share is a tuple containing:                                           *)
(* - process: the generating process                                        *)
(* - seq: sequence number of share generated by this process                *)
(* - work: work/difficulty in the share                                     *)
(****************************************************************************)
Share == [
    process: Processes,
    work: Work,
    bitcoin_height: (-1..MaxBitcoinHeight)
]

NoShare == [process |-> CHOOSE p \in Processes: TRUE, work |-> 0, bitcoin_height |-> -1]
        
TypeOK ==
    /\ shares \in [Processes -> Share]
    /\ parent \in [Share -> Share \cup {Genesis} \cup {NoShare}]
    /\ uncles \in [Share -> Share \cup {Genesis} \cup {NoShare}]
    /\ confirmed_work \in [Processes \X (Share \cup {Genesis}) -> Nat]
    /\ candidate_work \in [Processes \X (Share \cup {Genesis}) -> Nat]
    /\ expected_height \in [Processes \X Share -> Height \cup {NoHeight}]
    /\ candidates \in [Processes -> Seq(Share)]
    /\ confirmed \in [Processes -> Seq(Share)]
    /\ confirmed_uncles \in [Processes -> Seq(SUBSET Share)]
    /\ candidate_uncles \in [Processes -> Seq(SUBSET Share)]
    /\ received_shares \in [Processes -> Seq(Share)]
    /\ validation_status \in [Processes \X Share -> {"None", "Pending", "Valid", "Invalid"}]
    /\ share_queue \in Seq(Share)
    /\ bitcoin_height \in [Processes -> (0..MaxBitcoinHeight)]
    /\ spend_dependencies \in [Share -> SUBSET Share]

Init ==
    /\ shares = [p \in Processes |-> {Genesis}]  \* All processes start with genesis share
    /\ parent = [s \in Share \cup {Genesis} |-> NoShare]
    /\ uncles = [s \in Share \cup {Genesis} |-> {}]
    /\ confirmed_work = [p \in Processes, s \in (Share \cup {Genesis}) |-> IF s = Genesis THEN 1 ELSE 0] \* Genesis share has work of 1
    /\ candidate_work = [p \in Processes, s \in (Share \cup {Genesis}) |-> 0] \* Candidate chain is empty at the start
    /\ expected_height = [p \in Processes, s \in Share |-> IF s = Genesis THEN 1 ELSE NoHeight]
    /\ candidates = [p \in Processes |-> << >> ] \* Empty
    /\ confirmed = [p \in Processes |->  << Genesis >> ] \* Genesis at height 0
    /\ candidate_uncles = [p \in Processes |-> << >> ] \* Empty
    /\ confirmed_uncles = [p \in Processes |-> << >> ] \* Empty
    /\ received_shares = [p \in Processes |-> << >>]
    /\ validation_status = [p \in Processes, s \in Share |-> "None"]
    /\ bitcoin_height = [p \in Processes |-> 0]
    /\ share_queue = << >>
    /\ spend_dependencies = [s \in Share |-> {}]

(****************************************************************************)
(* Get the candidate top height for process p                               *)
(****************************************************************************)
TopConfirmed(p) ==
    confirmed[p][Len(confirmed[p])]

(****************************************************************************)
(* Get the penultimate confirmed height for process p                       *)
(****************************************************************************)
PenultimateConfirmed(p) ==
    confirmed[p][Len(confirmed[p]) - 1]

(****************************************************************************)
(* Get the candidate top height for process p                               *)
(****************************************************************************)
TopCandidate(p) ==
    IF Len(candidates[p]) = 0 THEN NoShare
    ELSE
    candidates[p][Len(candidates[p])]

(****************************************************************************)
(* Get the penultimate candidate height for process p                       *)
(****************************************************************************)
PenultimateCandidate(p) ==
    candidates[p][Len(candidates[p]) - 1]

(****************************************************************************)
(* Get the candidate height of a share s on process p                       *)
(* returns NoHeight if not found                                            *)
(* In implementaion we replace these scans with more efficient              *)
(* indexing of heights stored as "expected height derived from parent"      *)
(****************************************************************************)
GetCandidateHeight(p, s) ==
    SelectInSeq(candidates[p], LAMBDA v: v = s)

(****************************************************************************)
(* Get the confirmed height of a share s on process p                       *)
(* returns NoHeight if not found                                            *)
(* In implementaion we replace these scans with more efficient              *)
(* indexing of heights stored as "expected height derived from parent"      *)
(****************************************************************************)
GetConfirmedHeight(p, s) ==
    SelectInSeq(confirmed[p], LAMBDA v: v = s)

(****************************************************************************)
(* Recursively collect uncles from highest to lowest height, limiting to 3  *)
(****************************************************************************)
RECURSIVE CollectUncles(_, _, _, _, _)
CollectUncles(p, h, min_h, collected, over) ==
    IF h < min_h \/ Cardinality(collected) >= 3 THEN collected
    ELSE
    LET remaining == 3 - Cardinality(collected)
        uncles_at_h == over[p][h]
        \* Filter out shares that are already uncles in the range
        valid_uncles == {u \in uncles_at_h : 
            \A height \in min_h..h : u \notin uncles[p][height]}
        to_add == IF Cardinality(valid_uncles) <= remaining 
                THEN valid_uncles 
                ELSE CHOOSE subset \in SUBSET valid_uncles : Cardinality(subset) = remaining
    IN CollectUncles(p, h - 1, min_h, collected \cup to_add, over)

(****************************************************************************)
(* Get uncles for a share s generated by process p.                         *)
(* Find all uncles in confirmed uncles height with height between parent    *)
(* height - 3 and parent height                                             *)
(****************************************************************************)
ConfirmedUnclesFor(p, s) ==
    LET parent_height == GetConfirmedHeight(p, parent[s])
        min_height == IF parent_height > 3 THEN parent_height - 3 ELSE 1
        IN CollectUncles(p, parent_height, min_height, {}, confirmed_uncles)

(****************************************************************************)
(* Get uncles for a share s generated by process p from candidate chain.    *)
(* Find all uncles in candidate uncles height with height between parent    *)
(* height - 3 and parent height                                             *)
(****************************************************************************)
CandidateUnclesFor(p, s) ==
    LET parent_height == GetCandidateHeight(p, parent[s])
        min_height == IF parent_height > 3 THEN parent_height - 3 ELSE 1
        IN CollectUncles(p, parent_height, min_height, {}, candidate_uncles)

(****************************************************************************)
(* Push a share s onto the candidate chain for process p                    *)
(* - candidates is parent's candidates + 1                                  *)
(* - candidate_work is parent's candidate_work + s.work                     *)
(****************************************************************************)
PushToCandidate(p, s) ==
    LET parentShare == parent[s]
        parent_work == IF parentShare = NoShare \/ parentShare = Genesis THEN 1 ELSE candidate_work[p, parentShare]
    IN
        /\ candidates' = 
            [candidates EXCEPT ![p] = Append(@, s)]
        /\ candidate_work' = 
            [candidate_work EXCEPT ![p, s] = parent_work + s.work]

(****************************************************************************)
(* Push a share s onto the candidate uncles for process p                   *)
(* - candidate_uncles is parent's candidates + 1                            *)
(****************************************************************************)
PushToCandidateUncles(p, s) ==
    \* Parent must be in candidate height
    /\ 
        \/ GetCandidateHeight(p, parent[s]) # NoHeight
        \/ parent[s] = Genesis
    \* Parent must be no deeper than 3 from candidate top
    /\ Len(candidates[p]) - GetCandidateHeight(p, parent[s]) <= 3
    /\
        LET parentShare == parent[s]
            parent_height == GetCandidateHeight(p, parentShare)
            parent_work == IF parentShare = NoShare \/ parentShare = Genesis THEN 1 ELSE candidate_work[p, parentShare]
        IN  
            /\ IF parent_height = Len(candidate_uncles[p]) THEN 
                candidate_uncles' = 
                    [candidate_uncles EXCEPT ![p] = Append(@, {s})]
               ELSE
                candidate_uncles' = 
                    [candidate_uncles EXCEPT ![p][parent_height + 1] = @ \cup {s}]
            /\ candidate_work' = 
                [candidate_work EXCEPT ![p, s] = parent_work + s.work]

(****************************************************************************)
(* Push a share s onto the confirmed chain for process p                    *)
(* - confirmed is parent's confirmed + 1                                    *)
(* - confirmed_work is parent's confirmed_work + s.work                     *)
(****************************************************************************)
PushToConfirmed(p, s) ==
    \* Push only if parent is at the top of confirmed
    \* /\ parent[s] = TopConfirmed(p)
    /\
        LET parentShare == parent[s]
            parent_work == IF parentShare = NoShare \/ parentShare = Genesis THEN 1 ELSE confirmed_work[p, parentShare]
        IN
            /\ confirmed' = 
                [confirmed EXCEPT ![p] = Append(@, s)]
            /\ confirmed_work' = 
                [confirmed_work EXCEPT ![p, s] = parent_work + s.work]

(****************************************************************************)
(* Push a share s onto the confirmed uncles for process p                   *)
(* - confirmed_uncles is parent's confirmed + 1                             *)
(****************************************************************************)
PushToConfirmedUncles(p, s) ==
    \* Push only if parent is not at the top of confirmed
    /\ parent[s] # TopConfirmed(p)
    \* Parent must be in confirmed height
    /\ GetConfirmedHeight(p, parent[s]) # NoHeight
    \* Parent must be no deeper than 3 from confirmed top
    /\ Len(confirmed[p]) - GetConfirmedHeight(p, parent[s]) <= 3
    /\
        LET parentShare == parent[s]
            parent_height == GetConfirmedHeight(p, parentShare)
        IN  
            /\ confirmed_uncles' = 
                [confirmed_uncles EXCEPT ![p][parent_height + 1] = @ \cup {s}]
            /\ confirmed_work' = 
                [confirmed_work EXCEPT ![p, s] = confirmed_work[p, parentShare] + s.work]

(****************************************************************************)
(* Host process generates a new share                                       *)
(* - Creates a share with some work                                         *)
(* - Track parent for the new share as the current chain tip                *)
(* - Update tips to include the new share and remove the chain tip          *)
(****************************************************************************)
GenerateShare(p, work) ==
    /\ Cardinality(shares[p]) < MaxShares \* Limit total shares
    /\ Len(confirmed[p]) > 0 \* Ensure genesis is present
    /\ work \in Work
    /\ LET
        bitcoin_height_p == CHOOSE h \in (0..MaxBitcoinHeight) : h > bitcoin_height[p]
        newShare == [
            process |-> p,
            work |-> work,
            bitcoin_height |-> bitcoin_height_p
        ]
       IN  \* Add new share to the process's shares
           /\ shares' = [shares EXCEPT ![p] = @ \cup {newShare}]
            \* parent is the current chain tip
           /\ parent' = [parent EXCEPT ![newShare] = TopConfirmed(p)]
            \* Immediately confirm the new share
           /\ confirmed' = 
                [confirmed EXCEPT ![p] = Append(@, newShare)]
            \* Update confirmed work as parent's work + new share's work
           /\ confirmed_work' = 
                [confirmed_work EXCEPT ![p, newShare] = confirmed_work[p, TopConfirmed(p)] + work]
            \* select uncles for the new share
           /\ uncles' = [uncles EXCEPT ![newShare] = ConfirmedUnclesFor(p, newShare)]
           \* Enqueue the new share for sending to other processes
           /\ share_queue' = Append(share_queue, newShare)
           \* Set bitcoin height for the new share, it can be any valid height
           /\ bitcoin_height' = [bitcoin_height EXCEPT ![p] = bitcoin_height_p + 1]
           /\ UNCHANGED << received_shares, candidates, validation_status, spend_dependencies, candidate_work, confirmed_uncles, candidate_uncles, expected_height>>

(****************************************************************************)
(* Receive a share on process p                                             *)
(* The share is added to the shares set, but not  made candidate or         *)
(* confirmed yet                                                            *)
(****************************************************************************)
ReceiveShare(p) ==
    /\ share_queue # << >>
    /\ LET s == Head(share_queue)
       IN
        /\ \/ Contains(received_shares[p], s) # TRUE \* Only process share if not already received
           \/ s \notin shares[p] \* Only process share if not already present in stored shares
        /\ bitcoin_height[p] - s.bitcoin_height <= 1 \* Share should be recent enough
        \* Add to received shares
        /\ received_shares' = [received_shares EXCEPT ![p] = Append(@, s)]
    /\ share_queue' = Tail(share_queue)
    /\ UNCHANGED << parent, uncles, confirmed_work, confirmed, candidates, bitcoin_height, validation_status, shares, spend_dependencies, candidate_work, confirmed_uncles, candidate_uncles, expected_height>>


(****************************************************************************)
(* Validate a received share so it can be confirmed later                   *)
(* Validation is indepdent of parent's status                               *)
(* We could get into modeling txs, but that is not relevant to chain        *)
(* organisation                                                             *)
(****************************************************************************)
ValidateShare(p) ==
    /\ received_shares[p] # << >>
    /\ LET s == Head(received_shares[p])
       IN
        /\ \/ validation_status[p, s] = "None"
           \/ validation_status[p, s] = "Pending" 
        /\ validation_status' = [validation_status EXCEPT ![p, s] = "Valid"]
    /\ UNCHANGED << shares, parent, uncles, confirmed_work, confirmed, candidates, bitcoin_height, received_shares, share_queue, spend_dependencies, candidate_work, confirmed_uncles, candidate_uncles, expected_height >>

(****************************************************************************)
(* Set expected height for a share s on process p based on parent's         *)
(* expected height                                                          *)
(* We should do this only if share has valid PoW                            *)
(* Setting a height implies all parents of a share have been received       *)
(****************************************************************************)
SetHeight(p) ==
    /\ \E s \in Share:
        \* Share has been received
        /\ Contains(received_shares[p], s) = TRUE
        \* Share must not already have expected height set
        /\ expected_height[p, s] = NoHeight
        \* Parent has been assigned an expected height 
        /\ expected_height[p, parent[s]] # NoHeight
        /\
            LET parentShare == parent[s]
                parent_height == IF parentShare = NoShare \/ parentShare = Genesis THEN 1 ELSE expected_height[p, parentShare] + 1
            IN
                /\ expected_height' = [expected_height EXCEPT ![p, s] = parent_height]
        /\ UNCHANGED << shares, parent, uncles, confirmed_work, confirmed, candidates, bitcoin_height, received_shares, share_queue, validation_status, spend_dependencies, candidate_work, confirmed_uncles, candidate_uncles >>

(****************************************************************************)
(* Make a received share into a candidate share                             *)
(* - parent has to be a candidate or confirmed share                        *)
(* - candidates is parent's candidates + 1                                  *)
(****************************************************************************)
MakeShareCandidate(p) == 
    /\ received_shares[p] # << >>
    /\ LET s == Head(received_shares[p])
       IN
        /\ expected_height[p, s] # NoHeight                 \* Share must have expected height set
        /\ expected_height[p, s] = Len(candidates[p]) + 1   \* Expected height must be next in candidate chain
        /\ validation_status[p, s] = "Valid"
        \* Parent must be in candidate or confirmed chain
        /\  \/ Contains(candidates[p], parent[s]) 
            \/ Contains(confirmed[p], parent[s])
        /\ PushToCandidate(p, s)
        /\ PushToCandidateUncles(p, s)
        /\ received_shares' = [received_shares EXCEPT ![p] = Tail(@)]
    /\ UNCHANGED << shares, parent, uncles, confirmed_work, confirmed, bitcoin_height, share_queue, validation_status, spend_dependencies, confirmed_uncles, expected_height >>


Next ==
    \/ \E p \in Processes,
        work \in Work : GenerateShare(p, work)
    \/ \E p \in Processes : ReceiveShare(p)
    \/ \E p \in Processes : SetHeight(p)
    \/ \E p \in Processes : ValidateShare(p)
    \/ \E p \in Processes : MakeShareCandidate(p)
    \* \/ \E p \in Processes : ConfirmShare(p)

Fairness == 
    /\ WF_vars(\E p \in Processes: ReceiveShare(p))
    /\ WF_vars(\E p \in Processes: ValidateShare(p))
    /\ SF_vars(\E p \in Processes: received_shares[p] # << >> => ValidateShare(p))
    /\ SF_vars(\E p \in Processes: (\E s \in Share: validation_status[p, s] = "Valid") => MakeShareCandidate(p))
    /\ WF_vars(\E p \in Processes: MakeShareCandidate(p))
    \* /\ WF_vars(\E p \in Processes: ConfirmShare(p))

Spec == Init /\ [][Next]_vars /\ Fairness

THEOREM Spec => []TypeOK

====