---- MODULE ShareChain ----
EXTENDS Naturals, Sequences, TLC, FiniteSets, Integers, SequencesExt

CONSTANTS 
    Processes,                  \* Set of processes that generate shares
    MaxShares,                  \* Maximum number of shares per process
    Work,                       \* Work/difficulty parameter for shares
    MaxUnclesDepth,             \* Maximum depth for uncles included in a share
    MaxBitcoinHeight            \* Current Bitcoin network height for validating received shares

VARIABLES
    shares,                     \* Set of all shares across all processes
    chain_tip,                  \* Current tip of the chain per process
    parent,                     \* Parent references for each share
    uncles,                     \* Uncle references for each share
    chain_work,                 \* Total work accumulated up to each share on each process
    confirmed_height,           \* confirmed_height of each share in the chain at each process
    candidate_height,           \* candidate_height of each share in the chain at each process
    received_shares,            \* Set of shares received from other processes, but not validated yet
    validation_status,          \* Validation status of received shares
    share_queue,                \* Queue of shares that have been generated but not yet sent/received,
    bitcoin_height,              \* Bitcoin network height associated with each share
    spend_dependencies          \* Dependency Share -> Share. The key share is spending the value share.
vars == <<shares, chain_tip, share_queue, uncles, parent, chain_work, bitcoin_height, confirmed_height, candidate_height, received_shares, validation_status, spend_dependencies>>

(****************************************************************************)
(* Genesis share definition                                                *)
(****************************************************************************)

Genesis == [process |-> CHOOSE p \in Processes: TRUE, work |-> 1]

NoHeight == -1

(****************************************************************************)
(* A share is a tuple containing:                                           *)
(* - process: the generating process                                        *)
(* - seq: sequence number of share generated by this process                *)
(* - work: work/difficulty in the share                                     *)
(****************************************************************************)
Share == [
    process: Processes,
    work: Work,
    bitcoin_height: (0..MaxBitcoinHeight)
]

        
TypeOK ==
    /\ shares \in [Processes -> Share]
    /\ chain_tip \in [Processes -> {Share} \cup {Genesis}]
    /\ parent \in [Share -> Share \cup {Genesis}]
    /\ uncles \in [Share -> Share \cup {Genesis}]
    /\ chain_work \in [Processes \X Share -> Nat]
    /\ confirmed_height \in [Processes \X Share -> Nat \cup {NoHeight}]
    /\ candidate_height \in [Processes \X Share -> Nat \cup {NoHeight}]
    /\ received_shares \in [Processes -> Seq(Share)]
    /\ validation_status \in [Processes \X Share -> {"None", "Pending", "Valid", "Invalid"}]
    /\ share_queue \in Seq(Share)
    /\ bitcoin_height \in [Processes -> (0..MaxBitcoinHeight)]
    /\ spend_dependencies \in [Share -> SUBSET Share]

Init ==
    /\ shares = [p \in Processes |-> {Genesis}]  \* All processes start with genesis share
    /\ chain_tip = [p \in Processes |-> Genesis]
    /\ parent = [s \in Share \cup {Genesis} |-> {}]
    /\ uncles = [s \in Share \cup {Genesis} |-> {}]
    /\ chain_work = [p \in Processes, s \in Share \cup {Genesis} |-> IF s = Genesis THEN 1 ELSE 0] \* Genesis share has work of 1
    /\ confirmed_height = [p \in Processes, s \in Share \cup {Genesis} |-> IF s = Genesis THEN 0 ELSE NoHeight] \* Genesis share has confirmed_height 0
    /\ candidate_height = [p \in Processes, s \in Share \cup {Genesis} |-> NoHeight] \* Empty
    /\ received_shares = [p \in Processes |-> << >>]
    /\ validation_status = [p \in Processes, s \in Share |-> "None"]
    /\ bitcoin_height = [p \in Processes |-> 0]
    /\ share_queue = << >>
    /\ spend_dependencies = [s \in Share |-> {}]


(****************************************************************************)
(* Get the chain starting from given share for process p                    *)
(****************************************************************************)
RECURSIVE ChainFromShare(_, _)
ChainFromShare(p, s) ==
    IF s = Genesis THEN {}
    ELSE
        LET parentShare == parent[s]
        IN  {s} \cup ChainFromShare(p, parentShare)

(****************************************************************************)
(* Get the chain from share for process p - including uncles                *)
(****************************************************************************)
RECURSIVE DAG(_, _)
DAG(p, s) ==
    IF s = Genesis THEN {}
    ELSE
        LET parentShare == parent[s]
        IN  {s}
            \cup DAG(p, parentShare) 
            \cup {DAG(p, u) : u \in uncles[parentShare]}

(****************************************************************************)
(* Check if two shares have a common ancestor on process p within max uncle *)
(* depth                                                                    *)
(****************************************************************************)
ConfirmedCommonAncestorWithinRange(p, s1, s2) ==
    \E a \in shares[p] :
        /\ a \in ChainFromShare(p, s1)
        /\ a \in ChainFromShare(p, s2)
        /\ confirmed_height[p, a] # NoHeight
        /\ confirmed_height[p, chain_tip[p]] - confirmed_height[p, a] >= MaxUnclesDepth

(****************************************************************************)
(* Check if two shares have a common ancestor on process p                  *)
(****************************************************************************)
ConfirmedCommonAncestor(p, s1, s2) ==
    \E a \in shares[p] :
        /\ a \in ChainFromShare(p, s1)
        /\ a \in ChainFromShare(p, s2)
        /\ confirmed_height[p, a] # NoHeight

(****************************************************************************)
(* Get uncles for a share s generated by process p.                         *)
(* Uncles must meet the requirements:                                       *)
(* - Must be present in shares[p]                                           *)
(* - Must not be on the main chain                                          *)
(* - Must be no deeper than MaxUnclesDepth                                  *)
(* - Must have a common ancestor with the chain tip at p                    *)
(* - Must not already be included as an uncle                               *)
(****************************************************************************)
UnclesFor(p, s) ==
    {u \in shares[p] : 
        /\ u # s
        /\ u # chain_tip[p]
        /\ u \notin ChainFromShare(p, chain_tip[p])
        /\ confirmed_height[p, s] - confirmed_height[p, u] <= MaxUnclesDepth
        /\ ConfirmedCommonAncestorWithinRange(p, u, chain_tip[p])
        /\ u \notin UNION {uncles[x]: x \in shares[p]} \* u is not an uncle for any share tracked on p
    }

(****************************************************************************)
(* Host process generates a new share                                       *)
(* - Creates a share with some work                                         *)
(* - Track parent for the new share as the current chain tip                *)
(* - Update tips to include the new share and remove the chain tip          *)
(****************************************************************************)
GenerateShare(p, work) ==
    /\ Cardinality(shares[p]) < MaxShares \* Limit total shares
    /\ work \in Work
    /\ LET
        bitcoin_height_p == CHOOSE h \in (0..MaxBitcoinHeight) : h > bitcoin_height[p]
        newShare == [
            process |-> p,
            work |-> work,
            bitcoin_height |-> bitcoin_height_p
        ]
       IN  \* Add new share to the process's shares
           /\ shares' = [shares EXCEPT ![p] = @ \cup {newShare}]
            \* parent is the current chain tip
           /\ parent' = [parent EXCEPT ![newShare] = chain_tip[p]]
            \* select uncles for the new share
           /\ uncles' = [uncles EXCEPT ![newShare] = UnclesFor(p, newShare)]
           \* Update chain tip to the new share
           /\ chain_tip' = [chain_tip EXCEPT ![p] = newShare]
           \* The total work for the new share is the previous chain tip's total work plus the new share's work
           /\ chain_work' = [chain_work EXCEPT ![p, newShare] = chain_work[p, chain_tip[p]] + work]
           \* confirmed_height is previous chain tip's confirmed_height plus one
           /\ confirmed_height' = [confirmed_height EXCEPT ![p, newShare] = confirmed_height[p, chain_tip[p]] + 1]
           \* Enqueue the new share for sending to other processes
           /\ share_queue' = Append(share_queue, newShare)
           \* Set bitcoin height for the new share, it can be any valid height
           /\ bitcoin_height' = [bitcoin_height EXCEPT ![p] = bitcoin_height_p + 1]
           /\ UNCHANGED << received_shares, candidate_height, validation_status, spend_dependencies >>

(****************************************************************************)
(* Sum the chain work for a set of shares                                  *)
(****************************************************************************)
RECURSIVE SumWork(_, _)
SumWork(p, shareSet) ==
    IF shareSet = { } THEN 0
    ELSE LET s == CHOOSE x \in shareSet : TRUE
         IN chain_work[p, s] + SumWork(p, shareSet \ {s})

(****************************************************************************)
(* Calculate total work over PPLNS window for process p and share s        *)
(****************************************************************************)
TotalWorkOverPPLNSWindow(p, s) ==
    LET dag == DAG(p, s)
        IN  SumWork(p, dag)

(****************************************************************************)
(* Receive a share on process p                                             *)
(* The share is added to the shares set, but not  made candidate or         *)
(* confirmed yet                                                            *)
(****************************************************************************)
ReceiveShare(p) ==
    /\ share_queue # << >>
    /\ LET s == Head(share_queue)
       IN
        /\ \/ Contains(received_shares[p], s) # TRUE \* Only process share if not already received
           \/ s \notin shares[p] \* Only process share if not already present in stored shares
        /\ bitcoin_height[p] - s.bitcoin_height <= 1 \* Share should be recent enough
        \* Add to received shares
        /\ received_shares' = [received_shares EXCEPT ![p] = Append(@, s)]
    /\ share_queue' = Tail(share_queue)
    /\ UNCHANGED << chain_tip, parent, uncles, chain_work, confirmed_height, candidate_height, bitcoin_height, validation_status, shares, spend_dependencies >>


(****************************************************************************)
(* Validate a received share so it can be confirmed later                   *)
(* Validation is indepdent of parent's status                               *)
(* We could get into modeling txs, but that is not relevant to chain        *)
(* organisation                                                             *)
(****************************************************************************)
ValidateShare(p) ==
    /\ received_shares[p] # << >>
    /\ LET s == Head(received_shares[p])
       IN
        /\ \/ validation_status[p, s] = "None"
           \/ validation_status[p, s] = "Pending" 
        /\ validation_status' = [validation_status EXCEPT ![p, s] = "Valid"]
    /\ UNCHANGED << shares, chain_tip, parent, uncles, chain_work, confirmed_height, candidate_height, bitcoin_height, received_shares, share_queue, spend_dependencies >>
    
(****************************************************************************)
(* Make a received share into a candidate share                             *)
(* - parent has to be a candidate or confirmed share                        *)
(* - candidate_height is parent's candidate_height + 1                      *)
(****************************************************************************)
MakeShareCandidate(p) == 
    /\ received_shares[p] # << >>
    /\ LET s == Head(received_shares[p])
       IN
        /\  \/ candidate_height[p, parent[s]] # NoHeight \* Parent must be candidate or confirmed
            \/ confirmed_height[p, parent[s]] # NoHeight
        /\ validation_status[p, s] = "Valid" \* Only make candidate if share is valid
        /\ candidate_height' = [candidate_height EXCEPT ![p, s] = candidate_height[p, parent[s]] + 1]
        /\ received_shares' = [received_shares EXCEPT ![p] = Tail(@)]
    /\ UNCHANGED << shares, chain_tip, parent, uncles, chain_work, confirmed_height, bitcoin_height, share_queue, validation_status, spend_dependencies >>
    
\* MakeShareConfirmed(p)
\* UnconfirmShare(p)

(****************************************************************************)
(* Confirm a share on process p                                             *)
(* The receiving node's tips will be changed, the uncles and parent         *)
(* relationships remain unchanged.                                          *)
(****************************************************************************)
ConfirmShare(p) ==
    /\ share_queue # << >>
    /\ LET s == Head(share_queue)
       IN
        /\ s \notin shares[p] \* Only process share if not already present
        /\ bitcoin_height[p] - s.bitcoin_height <= 1 \* Share should be recent enough
        \* Add to shares
        /\ shares' = [shares EXCEPT ![p] = @ \cup {s}]
        \* Update confirmed_height to be parent's confirmed_height + 1
        /\ confirmed_height' = [confirmed_height EXCEPT ![p, s] = confirmed_height[p, parent[s]] + 1]
        \* Update chain work to be parent's chain work + s.work
        /\ chain_work' = [chain_work EXCEPT ![p, s] = chain_work[p, parent[s]] + s.work]
        /\ IF ConfirmedCommonAncestor(p, s, chain_tip[p]) THEN
            \* New share becomes the new tip if it exceeds current tip's work
            /\ IF chain_work[p, parent[s]] + s.work > chain_work[p, chain_tip[p]] THEN
                /\ chain_tip' = [chain_tip EXCEPT ![p] = s]
                /\ UNCHANGED <<parent, uncles, bitcoin_height, received_shares, candidate_height, validation_status, spend_dependencies>>
                ELSE
                /\ UNCHANGED <<chain_tip, parent, uncles, bitcoin_height, received_shares, candidate_height, validation_status, spend_dependencies>>
          ELSE
            \* For disjoint chains, the new share becomes the tip only if its total work over PPLNS window exceeds current tip's
            /\ IF TotalWorkOverPPLNSWindow(p, s) > TotalWorkOverPPLNSWindow(p, chain_tip[p]) THEN
                /\ chain_tip' = [chain_tip EXCEPT ![p] = s]
                /\ UNCHANGED <<parent, uncles, bitcoin_height, received_shares, candidate_height, validation_status, spend_dependencies>>
               ELSE
                /\ UNCHANGED <<chain_tip, parent, uncles, bitcoin_height, received_shares, candidate_height, validation_status, spend_dependencies>>
    /\ share_queue' = Tail(share_queue)

Next ==
    \/ \E p \in Processes,
        work \in Work : GenerateShare(p, work)
    \/ \E p \in Processes : ReceiveShare(p)
    \/ \E p \in Processes : ValidateShare(p)
    \/ \E p \in Processes : MakeShareCandidate(p)
    \/ \E p \in Processes : ConfirmShare(p)

Fairness == /\ WF_vars(\E p \in Processes: ConfirmShare(p))
            /\ WF_vars(\E p \in Processes: ReceiveShare(p))
            /\ WF_vars(\E p \in Processes: ValidateShare(p))
            /\ WF_vars(\E p \in Processes: MakeShareCandidate(p))

Spec == Init /\ [][Next]_vars /\ Fairness

THEOREM Spec => []TypeOK

====