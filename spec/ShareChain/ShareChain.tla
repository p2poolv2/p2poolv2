---- MODULE ShareChain ----
EXTENDS Naturals, Sequences, TLC, FiniteSets, Integers, SequencesExt

CONSTANTS 
    Processes,                  \* Set of processes that generate shares
    MaxShares,                  \* Maximum number of shares per process
    Work,                       \* Work/difficulty parameter for shares
    MaxUnclesDepth,             \* Maximum depth for uncles included in a share
    MaxUncles,                  \* Maximum number of uncles included in a share
    MaxBitcoinHeight            \* Current Bitcoin network height for validating received shares

VARIABLES
    stored_shares,              \* Set of all shares across all processes
    parent,                     \* Parent references for each share
    uncles,                     \* Uncle references for each share
    chain_work,                 \* Total work accumulated up to each candidate height, if any
    expected_height,            \* expected height of each share in the chain at each process.
                                \*Shares may or may not candidate or confirm to this height
    candidates,                 \* candidates of each share in the chain at each process
    confirmed,                  \* confirmed of each share in the chain at each process
    received_shares,            \* Queue of shares received from other processes, but not validated yet
    validation_status,          \* Validation status of received shares
    share_status,               \* Candidate/Confirmed status of shares
    share_queue,                \* Queue of shares that have been generated but not yet sent to peers,
    bitcoin_height,             \* Bitcoin network height associated with each share
    spend_dependencies          \* Dependency Share -> Share. The key share is spending the value share.
vars == <<stored_shares, share_queue, uncles, parent, bitcoin_height, 
          confirmed, candidates, received_shares, validation_status, 
          spend_dependencies, chain_work, expected_height, share_status>>

(****************************************************************************)
(* Genesis share definition                                                 *)
(****************************************************************************)
Genesis == [process |-> CHOOSE p \in Processes: TRUE, work |-> 1, bitcoin_height |-> 0]

(****************************************************************************)
(* Use NoHeight to indicate share not found in height sequences             *)
(* Sequences are indexed from 1                                             *)
(****************************************************************************)
NoHeight == 0

(****************************************************************************)
(* Total shares across all processes                                        *)
(****************************************************************************)
TotalShares == MaxShares * Cardinality(Processes)

(****************************************************************************)
(* Genesis height is 1, aligns with sequence indexed from 1                 *)
(****************************************************************************)
Height == (1..TotalShares)

(****************************************************************************)
(* A share is a tuple containing:                                           *)
(* - process: the generating process                                        *)
(* - seq: sequence number of share generated by this process                *)
(* - work: work/difficulty in the share                                     *)
(****************************************************************************)
Share == [
    process: Processes,
    work: Work,
    bitcoin_height: (-1..MaxBitcoinHeight)
]

(****************************************************************************)
(* Define NoShare to indicate absence of a share                            *)
(****************************************************************************)
NoShare == [process |-> CHOOSE p \in Processes: TRUE, work |-> 0, bitcoin_height |-> -1]

(****************************************************************************)
(* Type definitions for all variables                                       *)
(****************************************************************************)
TypeOK ==
    /\ stored_shares \in [Processes -> SUBSET Share]
    /\ parent \in [Share -> Share \cup {Genesis} \cup {NoShare}]
    /\ uncles \in [Share -> SUBSET {Share \cup {Genesis} \cup {NoShare}}]
    /\ chain_work \in [Processes \X (Share \cup {Genesis}) -> Nat]
    /\ expected_height \in [Processes \X Share \cup {Genesis} -> Height \cup {NoHeight}]
    /\ candidates \in [Processes -> Seq(Share)]
    /\ confirmed \in [Processes -> Seq(Share)]
    /\ received_shares \in [Processes -> Seq(Share)]
    /\ validation_status \in [Processes \X Share -> {"None", "Pending", "Valid", "Invalid"}]
    /\ share_status \in [Processes \X Share -> {"None", "Candidate", "Confirmed"}]
    /\ share_queue \in Seq(Share)
    /\ bitcoin_height \in [Processes -> (0..MaxBitcoinHeight)]
    /\ spend_dependencies \in [Share -> SUBSET Share]

(****************************************************************************)
(* Initial state definition                                                 *)
(****************************************************************************)
Init ==
    \* All processes start with genesis share
    /\ stored_shares = [p \in Processes |-> {Genesis}] 
    /\ parent = [s \in Share \cup {Genesis} |-> NoShare]
    /\ uncles = [s \in Share \cup {Genesis} |-> {}]
    \* Candidate chain is empty at the start
    /\ chain_work = [p \in Processes, s \in (Share \cup {Genesis}) |-> 0]
    /\ expected_height = [p \in Processes, s \in Share |-> 
                            IF s = Genesis THEN 1 ELSE NoHeight]
    /\ candidates = [p \in Processes |-> << >> ]            \* Empty
    /\ confirmed = [p \in Processes |->  << Genesis >> ]    \* Genesis at height 1
    /\ received_shares = [p \in Processes |-> << >>]
    /\ validation_status = [p \in Processes, s \in Share |-> "None"]
    /\ share_status = [p \in Processes, s \in Share |-> "None"]
    /\ bitcoin_height = [p \in Processes |-> 0]
    /\ share_queue = << >>
    /\ spend_dependencies = [s \in Share |-> {}]

(****************************************************************************)
(* Get the top confirmed share for process p                               *)
(****************************************************************************)
TopConfirmed(p) == confirmed[p][Len(confirmed[p])]

(****************************************************************************)
(* Get the top candidate share for process p                                *)
(****************************************************************************)
TopCandidate(p) ==
    IF Len(candidates[p]) = 0 THEN NoShare
    ELSE
    candidates[p][Len(candidates[p])]

(****************************************************************************)
(* Get the candidate height of a share s on process p                       *)
(* returns NoHeight if not found                                            *)
(* In implementation we replace these scans with more efficient             *)
(* indexing of heights stored as "expected height derived from parent"      *)
(****************************************************************************)
GetCandidateHeight(p, s) == SelectInSeq(candidates[p], LAMBDA v: v = s)

(****************************************************************************)
(* Get the confirmed height of a share s on process p                       *)
(* returns NoHeight if not found                                            *)
(* In implementation we replace these scans with more efficient             *)
(* indexing of heights stored as "expected height derived from parent"      *)
(****************************************************************************)
GetConfirmedHeight(p, s) == SelectInSeq(confirmed[p], LAMBDA v: v = s)

(****************************************************************************)
(* Get uncles for a share s generated by process p.                         *)
(* - Find all Candidate shares with parents in confirmed with depth >= 3    *)
(* - filter out those that are not uncles of any other shares               *)
(* - choose any max 3 shares                                                *)
(* By selecting uncles using only parent in confirmed index check we avoid  *)
(* tracking candidate/confirmed uncles separately and thus don't have to    *)
(* handle such new indexes during chain reorgs                              *)
(* Remember in p2poolv2 uncles transactions are ignored, only main chain    *)
(* transactions impact the inputs/output states                             *)
(****************************************************************************)
UnclesFor(p, s) ==
    LET parent_confirmed_height == GetConfirmedHeight(p, parent[s])
        min_depth == IF parent_confirmed_height > MaxUnclesDepth
                     THEN parent_confirmed_height - MaxUnclesDepth
                     ELSE 1
        \* Get all shares that have parents in confirmed chain within depth range
        all_uncles == UNION {uncles[sh] : sh \in DOMAIN uncles}
        potential_uncles == {u \in stored_shares[p] : 
            /\ u # s
            /\ GetConfirmedHeight(p, parent[u]) # NoHeight
            /\ GetConfirmedHeight(p, parent[u]) >= min_depth
            /\ GetConfirmedHeight(p, parent[u]) < parent_confirmed_height
            /\ u \notin all_uncles}
    \* Select up to MaxUncles uncles
    IN IF Cardinality(potential_uncles) <= MaxUncles 
       THEN potential_uncles
       ELSE CHOOSE subset \in SUBSET potential_uncles : Cardinality(subset) = MaxUncles
    

(****************************************************************************)
(* Host process generates a new share                                       *)
(* - Creates a share with some work                                         *)
(* - Track parent for the new share as the current chain tip                *)
(* - Update tips to include the new share and remove the chain tip          *)
(****************************************************************************)
GenerateShare(p, work) ==
    /\ Cardinality(stored_shares[p]) < MaxShares           \* Limit total shares
    /\ Len(confirmed[p]) > 0                               \* Ensure genesis is present
    /\ work \in Work
    /\ LET
        bitcoin_height_p == CHOOSE h \in (0..MaxBitcoinHeight) : 
                                   h > bitcoin_height[p]
        newShare == [
            process |-> p,
            work |-> work,
            bitcoin_height |-> bitcoin_height_p
        ]
       IN  \* Add new share to the process's shares
           /\ stored_shares' = [stored_shares EXCEPT ![p] = @ \cup {newShare}]
            \* parent is the current chain tip
           /\ parent' = [parent EXCEPT ![newShare] = TopConfirmed(p)]
            \* Immediately confirm the new share
           /\ confirmed' = 
                [confirmed EXCEPT ![p] = Append(@, newShare)]
            \* Update confirmed work as parent's work + new share's work
           /\ chain_work' = 
                [chain_work EXCEPT ![p, newShare] =
                    chain_work[p, TopConfirmed(p)] + work]
            \* select uncles for the new share
           /\ uncles' = [uncles EXCEPT ![newShare] = UnclesFor(p, newShare)]
           \* Enqueue the new share for sending to other processes
           /\ share_queue' = Append(share_queue, newShare)
           \* Set bitcoin height for the new share, it can be any valid height
           /\ bitcoin_height' = [bitcoin_height EXCEPT ![p] = bitcoin_height_p]
           /\ UNCHANGED << received_shares, candidates, validation_status, 
                           spend_dependencies, expected_height, share_status >>

(****************************************************************************)
(* Receive a share on process p                                             *)
(* The share is added to the stored_shares set, but not  made candidate or  *)
(* confirmed yet                                                            *)
(****************************************************************************)
ReceiveShare(p) ==
    /\ share_queue # << >>
    /\ LET s == Head(share_queue)
       IN
        /\ /\ ~Contains(received_shares[p], s)                  \* Only process share if not already received
           /\ s \notin stored_shares[p]                         \* and not already present in stored shares
        /\ bitcoin_height[p] - s.bitcoin_height <= 1            \* Share should be recent enough
        \* Add to received shares
        /\ received_shares' = [received_shares EXCEPT ![p] = Append(@, s)]
    /\ share_queue' = Tail(share_queue)
    /\ UNCHANGED << parent, uncles, confirmed, candidates, bitcoin_height, 
                    validation_status, stored_shares, spend_dependencies, chain_work,
                    expected_height, share_status>>

(****************************************************************************)
(* Validate a received share so it can be confirmed later                   *)
(* Validation is independent of parent's status                             *)
(* We could get into modeling txs, but that is not relevant to chain        *)
(* organisation                                                             *)
(****************************************************************************)
ValidateShare(p, s) ==
    /\ received_shares[p] # << >>
    /\ Contains(received_shares[p], s)
    /\ \/ validation_status[p, s] = "None"
        \/ validation_status[p, s] = "Pending" 
    /\ validation_status' = [validation_status EXCEPT ![p, s] = "Valid"]
    /\ UNCHANGED << stored_shares, parent, uncles, confirmed, candidates, bitcoin_height,
                    received_shares, share_queue, spend_dependencies, chain_work,
                    expected_height, share_status >>

(****************************************************************************)
(* Set expected height for a share s on process p based on parent's         *)
(* expected height                                                          *)
(* We should do this only if share has valid PoW                            *)
(* Setting a height implies all parents of a share have been received       *)
(****************************************************************************)
StoreShareAndSetExpectedHeight(p, s) ==
    \* Share has been received
    /\ Contains(received_shares[p], s)
    \* Share must not already have expected height set
    /\ expected_height[p, s] = NoHeight
    \* Parent has been assigned an expected height 
    /\ \/ parent[s] = Genesis
       \/ parent[s] = NoShare
       \/ expected_height[p, parent[s]] # NoHeight
    /\
        LET parentShare == parent[s]
            parent_height == IF parentShare = NoShare \/ parentShare = Genesis
                                THEN 1 
                                ELSE expected_height[p, parentShare]
        IN
            /\ stored_shares' = [stored_shares EXCEPT ![p] = @ \cup {s}]
                \* Set expected height for the share
            /\ expected_height' = [expected_height EXCEPT ![p, s] = parent_height + 1]
    /\ UNCHANGED << parent, uncles, confirmed, candidates, bitcoin_height, 
                    received_shares, share_queue, validation_status,
                    spend_dependencies, chain_work, share_status >>

(****************************************************************************)
(* Mark a received share as candidate without appending to candidate chain  *)
(* or marking it as an orphan share                                         *)
(* - parent has to be a candidate or confirmed share but not at top         *)
(* - share is not added to candidate chain                                  *)
(* - share status is updated to "Candidate"                                 *)
(* - candidate work is updated for the share                                *)
(* - share is not removed from received shares queue                        *)
(****************************************************************************)
MarkShareAsCandidate(p, s) ==
    /\ received_shares[p] # << >>
    /\ s = Head(received_shares[p])
    \* Share must have expected height set
    /\ expected_height[p, s] # NoHeight
    \* Share must be valid
    /\ validation_status[p, s] = "Valid"
    \* Share must not already be candidate or confirmed
    /\ share_status[p, s] = "None"
    \* Share must not already be candidate or confirmed
    /\ /\ ~Contains(candidates[p], s)
       /\ ~Contains(confirmed[p], s)
    \* Parent must be in candidate or confirmed chain
    /\ \/ Contains(candidates[p], parent[s])
       \/ Contains(confirmed[p], parent[s])
    /\ share_status' = [share_status EXCEPT ![p, s] = "Candidate"]
    /\ chain_work' = [chain_work EXCEPT ![p, s] = 
            IF parent[s] = Genesis \/ parent[s] = NoShare THEN 1 
            ELSE chain_work[p, parent[s]] + s.work]
    /\ UNCHANGED << stored_shares, parent, uncles, confirmed, candidates,
                    bitcoin_height, share_queue, validation_status,
                    spend_dependencies, expected_height, received_shares >>

(****************************************************************************)
(* Append a received share to the top of candidates                         *)
(* - parent has to be top candidate or top confirmed share                  *)
(* - candidate is added to index at the top                                 *)
(* - share is removed from received shares queue                            *)
(****************************************************************************)
AppendShareToCandidates(p, s) == 
    /\ received_shares[p] # << >>
    /\ s = Head(received_shares[p])
    \* Share must have expected height set
    /\ expected_height[p, s] # NoHeight
    \* Share must be valid
    /\ validation_status[p, s] = "Valid"
    \* Share must have Candidate status
    /\ share_status[p, s] = "Candidate"
    \* Parent must be top candidate or top confirmed share
    \* include other sanity checks for height and work
    /\ \/ (/\ TopCandidate(p) = parent[s]
           /\ expected_height[p, s] = Len(candidates[p]) + 1 
           /\ chain_work[p, s] > chain_work[p, TopCandidate(p)])
       \/ (/\ TopConfirmed(p) = parent[s]
           /\ expected_height[p, s] = Len(confirmed[p]) + 1 
           /\ chain_work[p, s] > chain_work[p, TopConfirmed(p)])
    /\ candidates' = [candidates EXCEPT ![p] = Append(@, s)]
    /\ received_shares' = [received_shares EXCEPT ![p] = Tail(@)]
    /\ UNCHANGED << stored_shares, parent, uncles, confirmed, bitcoin_height,
                    share_queue, validation_status, spend_dependencies,
                    expected_height, share_status, chain_work >>

(****************************************************************************)
(* Make a received share into an orphan share - it can't go to top of       *) 
(* candidates.                                                              *)
(* - parent has to be in confirmed chain, but not top confirmed share       *)
(* - the chain work for the share should be less than or equal to top       *)
(*   confirmed                                                              *)   
(* - share is removed from received shares queue                            *)
(* - orphans/uncles are never confirmed as we don't consider the            *)
(*   included tx in them. Uncles are only used for share accounting.        *)
(* - These orphans are potentially added as uncles when a peer generates a  *)
(*   share that includes them as uncles                                     *)
(* - The status is left as Candidate, for any potential future candidate    *)
(*   reorgs                                                                 *)
(****************************************************************************)
MarkShareAsOrphan(p, s) ==
    /\ received_shares[p] # << >>
    /\ s = Head(received_shares[p])
    /\ expected_height[p, s] # NoHeight         \* Share must have expected height set
    /\ validation_status[p, s] = "Valid"
    /\ share_status[p, s] = "Candidate"         \* Share must be marked as candidate
    /\ Contains(confirmed[p], parent[s])        \* Parent must be in confirmed chain
    /\ parent[s] # TopConfirmed(p)              \* Parent must not be top confirmed
    \* Share must have less or equal work than top confirmed
    /\ chain_work[p, s] <= chain_work[p, TopConfirmed(p)]
    /\ received_shares' = [received_shares EXCEPT ![p] = Tail(@)]
    /\ UNCHANGED << stored_shares, parent, uncles, confirmed, candidates,
                    bitcoin_height, share_queue, validation_status,
                    spend_dependencies, expected_height, 
                    share_status, chain_work>>

(****************************************************************************)
(* Get the chain starting from given share for process p to the first       *)
(* candidate (branch point) - in reverse                                    *)
(* Note the chain is not explicitly required to be stored anywhere, it is   *)
(* constructed by following parent links recursively                        *)
(* Returns Seq << branch point .. s >>                                      *)
(****************************************************************************)
RECURSIVE CandidateBranch(_, _)
CandidateBranch(p, s) ==
    IF \/ s = Genesis                  \* genesis as end of candidate chain
       \/ Contains(candidates[p], s)   \* ancestor in candidate chain
    THEN 
        << s >>
    ELSE 
        CandidateBranch(p, parent[s]) \o << s >>

(****************************************************************************)
(* For a share with Candidate status and more work than top, reorg          *)
(* - get a share that has more work than top candidate, is marked Candidate *)
(* but not yet in candidates chain                                          *)
(* - find common ancestor between top candidate and the new share, call it  *)
(*   branch point                                                           *)
(* - remove old chain, the chain from top candidate to branch point from    *)
(*   the candidate index                                                    *)
(* - push chain from branch point to the new share on to candidate index    *)
(****************************************************************************)
ReorgCandidateChain(p, s) ==
    \* /\ Contains(received_shares[p], s)
    \* Share must not be in candidate chain
    /\ ~Contains(candidates[p], s)
    \* Share must have Candidate status
    /\ share_status[p, s] = "Candidate"
    \* Share must be valid
    /\ validation_status[p, s] = "Valid"
    \* Share must have expected height set
    /\ expected_height[p, s] # NoHeight
    \* There must be existing candidates to reorg against, else we just AppendShareToCandidates
    /\ Len(candidates[p]) > 0
    \* Share must have more work than top candidate. With same work, we don't reorg
    /\ chain_work[p, s] > chain_work[p, TopCandidate(p)]
    \* Find branch point
    /\ LET  candidate_branch == CandidateBranch(p, s)
            branchPoint == Head(candidate_branch)
            reorged_out_chain == 
                SubSeq(candidates[p],
                    GetCandidateHeight(p, branchPoint),
                    GetCandidateHeight(p, TopCandidate(p))) 
        IN  
            /\ \* Replace candidates chain with new chain from genesis -> branch point -> new tip
                candidates' = 
                [candidates EXCEPT ![p] = 
                    SubSeq(@,1, GetCandidateHeight(p, parent[branchPoint])) \o candidate_branch]
    /\ received_shares' = [received_shares EXCEPT ![p] = Tail(@)]
    /\ UNCHANGED << stored_shares, parent, uncles, confirmed, bitcoin_height,
                    share_queue, validation_status, spend_dependencies,
                    chain_work, expected_height, share_status >>

(****************************************************************************)
(* Confirm the candidate chain up to top candidate                          *)
(* - Require that no shares in the candidate chain have spending dependency *)
(*   on a share in the future                                               *)
(* - Move all candidates to confirmed chain                                 *)
(* - Note: We don't track candidate/confirmation state of uncles. The state *)
(*   of their parent on the candidate/confirmed chain is what matters       *) 
(****************************************************************************)
ConfirmCandidateChain(p) ==
    /\ Len(candidates[p]) > 0
    /\ \A i \in 1..Len(candidates[p]) :
        \A dep \in spend_dependencies[candidates[p][i]] :
            GetCandidateHeight(p, dep) < GetCandidateHeight(p, candidates[p][i])
    /\ LET new_confirmed == candidates[p]
       IN  
       \* Make candidate chain the new confirmed chain
        /\ confirmed' = [confirmed EXCEPT ![p] = confirmed[p] \o new_confirmed]
        \* Clear candidates
        /\ candidates' = [candidates EXCEPT ![p] = << >>]
    /\ UNCHANGED << stored_shares, parent, uncles, bitcoin_height, share_queue,
                    received_shares, validation_status, spend_dependencies,
                    chain_work, expected_height, share_status >>

(****************************************************************************)
(* Next-state relation                                                      *)
(****************************************************************************)
Next ==
    \/ \E p \in Processes,
        work \in Work : GenerateShare(p, work)
    \/ \E p \in Processes : ReceiveShare(p)
    \/ \E p \in Processes, s \in Share : StoreShareAndSetExpectedHeight(p, s)
    \/ \E p \in Processes, s \in Share : ValidateShare(p, s)
    \/ \E p \in Processes, s \in Share : AppendShareToCandidates(p, s)
    \/ \E p \in Processes, s \in Share : MarkShareAsCandidate(p, s)
    \/ \E p \in Processes, s \in Share : ReorgCandidateChain(p, s)
    \/ \E p \in Processes : ConfirmCandidateChain(p)
    \/ \E p \in Processes, s \in Share: MarkShareAsOrphan(p, s)

(****************************************************************************)
(* Fairness constraints that must be met by the spec                        *)
(****************************************************************************)
Fairness == 
    /\ WF_vars(\E p \in Processes: ReceiveShare(p))
    /\ WF_vars(\E p \in Processes, s \in Share: ValidateShare(p, s))
    /\ WF_vars(\E p \in Processes, s \in Share: StoreShareAndSetExpectedHeight(p, s))
    /\ WF_vars(\E p \in Processes, s \in Share: AppendShareToCandidates(p, s))
    /\ WF_vars(\E p \in Processes, s \in Share: MarkShareAsCandidate(p, s))
    /\ WF_vars(\E p \in Processes, s \in Share: ReorgCandidateChain(p, s))
    /\ WF_vars(\E p \in Processes: ConfirmCandidateChain(p))
    /\ WF_vars(\E p \in Processes, s \in Share: MarkShareAsOrphan(p, s))

Spec == Init /\ [][Next]_vars /\ Fairness

THEOREM Spec => []TypeOK

====